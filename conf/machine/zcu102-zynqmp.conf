#@TYPE: Machine
#@NAME: zynqmp-zcu102
#@DESCRIPTION: Machine support for ZCU102 Evaluation Board.
#

require conf/machine/include/tune-zynqmp.inc
require conf/machine/include/machine-xilinx-default.inc
require conf/machine/include/machine-xilinx-board.inc

MACHINE_FEATURES = "rtc ext2 ext3 vfat usbhost mali"

# This machine has a qemu machine model
EXTRA_IMAGEDEPENDS += "qemu-native qemu-helper-native"

UBOOT_MACHINE = "xilinx_zynqmp_zcu102_revB_defconfig"

SERIAL_CONSOLE = "115200 ttyPS0"

KERNEL_DEVICETREE = "xilinx/zynqmp-zcu102-revB.dtb"

PREFERRED_PROVIDER_virtual/kernel ?= "linux-xlnx"
PREFERRED_PROVIDER_virtual/bootloader ?= "u-boot-xlnx"

EXTRA_IMAGEDEPENDS += "\
		arm-trusted-firmware \
		"

# For runqemu
IMAGE_CLASSES += "qemuboot"
QB_SYSTEM_NAME = "qemu-system-aarch64"
QB_MACHINE = "-machine xlnx-zcu102"
QB_MEM = "-m 2048"
QB_DTB = "${KERNEL_IMAGETYPE}-${@os.path.splitext(os.path.basename(d.getVar("KERNEL_DEVICETREE",True)))[0]}.dtb"
QB_OPT_APPEND = "-nographic -serial mon:stdio -serial null"
QB_DEFAULT_FSTYPE = "cpio"
QB_KERNEL_CMDLINE_APPEND = "console=ttyPS0"
QB_TAP_OPT = "-netdev tap,id=net0,ifname=@TAP@,script=no,downscript=no -net nic -net nic -net nic -net nic,netdev=net0,macaddr=@MAC@"
QB_SLIRP_OPT = "-netdev user,id=net0 -net nic -net nic -net nic -net nic,netdev=net0"

